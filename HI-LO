//@version=3

// Indicator: Period-Bounded High/Low Lines
//
// Essentially an implementation of an indicator used by Adam Grimes
// in this Chartschool video, Anticipating Intraday Action:
//
// https://www.youtube.com/watch?v=NdLCt1BoWp0
//
// Thought it was pretty neat.
//
// Just with added options for other common timeframe bounds.
//
// Use the "Daily" bound with intra-day interval charts to
// get the same effect as in the video.
//
// Author: Kamal Advani / ka66
//

study(title="HI-LO", shorttitle="20HL", overlay=true)

DAILY = "Daily"
WEEKLY = "Weekly"
MONTHLY = "Monthly"
YEARLY = "Yearly"


//
// Inputs
//

hiLoPeriod = input(title="Hi/Lo Reset Period", type=string, defval=DAILY, options=[DAILY, WEEKLY, MONTHLY, YEARLY])


//
// Logic
//

getResetPeriodSeries(periodTypeDesc) =>
    if periodTypeDesc == DAILY
        dayofweek
    else
        if periodTypeDesc == WEEKLY
            weekofyear
        else
            if periodTypeDesc == MONTHLY
                month
            else // YEARLY
                year

resetPeriodSeries = getResetPeriodSeries(hiLoPeriod)
highsInPeriod = high
lowsInPeriod = low

requiresReset = resetPeriodSeries != nz(resetPeriodSeries[1])

highsInPeriod := if requiresReset
    // reset to new period's high
    high
else
    // compare subsequent period highs
    high > nz(highsInPeriod[1]) ? high : highsInPeriod[1]

lowsInPeriod := if requiresReset
    // reset to new period's low
    low
else
    // compare subsequent period lows
    low < nz(lowsInPeriod[1]) ? low : lowsInPeriod[1]


//
// Plot Visuals
//

style = circles
hiColor = #aaaaff
loColor = #ffaaaa
lineWidth = 2

plot(highsInPeriod, title='hi lines', color=hiColor, linewidth=lineWidth, style=style)
plot(lowsInPeriod, title='lo lines', color=loColor, linewidth=lineWidth, style=style)
